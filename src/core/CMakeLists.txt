file(GLOB CORE_SOURCES CONFIGURE_DEPENDS *.c)

add_library(${CORE_LIB} STATIC ${CORE_SOURCES})

target_include_directories(${CORE_LIB} PUBLIC
    ${CMAKE_CURRENT_SOURCE_DIR}
)

set(DEFAULT_INSTRUCTIONS_PER_SECOND 700 CACHE STRING "Default CPU cycles per second")
set(DEFAULT_FONT FONT_CHIP48 CACHE STRING "Default font to load")
option(ENABLE_LOGS "Enable runtime logging" OFF)
option(LEGACY_OFFSET_JUMP_BEHAVIOR "Use legacy jump with offset behavior" ON)
option(LEGACY_MEMORY_BEHAVIOR "Use legacy memory behavior" OFF)
option(LEGACY_SHIFT_BEHAVIOR "Use legacy shift behavior" ON)

target_compile_definitions(${CORE_LIB}
  PUBLIC
    INSTRUCTIONS_PER_SECOND=${DEFAULT_INSTRUCTIONS_PER_SECOND}
    DEFAULT_FONT=${DEFAULT_FONT}
    $<$<BOOL:${ENABLE_LOGS}>:ENABLE_LOGS>
    $<$<BOOL:${LEGACY_OFFSET_JUMP_BEHAVIOR}>:LEGACY_OFFSET_JUMP_BEHAVIOR>
    $<$<BOOL:${LEGACY_MEMORY_BEHAVIOR}>:LEGACY_MEMORY_BEHAVIOR>
    $<$<BOOL:${LEGACY_SHIFT_BEHAVIOR}>:LEGACY_SHIFT_BEHAVIOR>
)
